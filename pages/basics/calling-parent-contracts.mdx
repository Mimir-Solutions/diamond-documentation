import Callout from 'nextra-theme-docs/callout'

# Calling Parent Contracts

```jsx
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.13;

contract A {
    // This is called an event. You can emit events from your function
    // and they are logged into the transaction log.
    // In our case, this will be useful for tracing function calls.
    event Log(string message);

    function get() public virtual {
        emit Log("A.get called");
    }

    function set() public virtual {
        emit Log("A.set called");
    }
}

contract B is A {
    function get() public virtual override {
        emit Log("B.get called");
        A.get();
    }

    function set() public virtual override {
        emit Log("B.set called");
        super.set();
    }
}

contract C is A {
    function get() public virtual override {
        emit Log("C.get called");
        A.get();
    }

    function set() public virtual override {
        emit Log("C.set called");
        super.set();
    }
}

contract D is B, C {

    function get() public override(B, C) {
        super.get();
    }

    function set() public override(B, C) {
        super.set();
    }
}
```

Try:
- Call D.get and check the transaction logs.
- Although D inherits A, B and C, it only called C and then A.
```plantext
"C.get called"
"A.get called"
```
- Call D.set and check the transaction logs D called C, then B, and finally A.
- Although `super` was called twice (by B and C) it only called A once.
```plantext
"C.set called"
"B.set called"
"A.set called"
```