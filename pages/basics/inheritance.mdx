import Callout from 'nextra-theme-docs/callout'

# Inheritance
- You can write multiple contracts and inherit into a single one.
- Contracts can inherit other contract by using the `is` keyword.
- The contract from where other contracts inherit the features called base contract.
- The contract which inherits the features known as a derived contract—in a slightly different language, people often referred it as a parent and child contract, respectively.
- Function that is going to be overridden by a child contract must be declared as `virtual`.
- Function that is going to override a parent function must use the keyword `override`.
- Order of inheritance is important.

```jsx
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.13;

contract A {
    function get() public pure virtual returns (string memory) {
        return "A";
    }
}

// Contracts inherit other contracts by using the keyword 'is'.
contract B is A {
    // Override A.get()
    function get() public pure virtual override returns (string memory) {
        return "B";
    }
}

contract C is A {
    // Override A.get()
    function get() public pure virtual override returns (string memory) {
        return "C";
    }
}

// Contracts can inherit from multiple parent contracts.
// When a function is called that is defined multiple times in
// different contracts, parent contracts are searched from
// right to left, and in depth-first manner.

contract D is B, C {
    // D.get() returns "C"
    // since C is the right most parent contract with function get()
    function get() public pure override(B, C) returns (string memory) {
        return super.get();
    }
}

contract E is C, B {
    // E.get() returns "B"
    // since B is the right most parent contract with function get()
    function get() public pure override(C, B) returns (string memory) {
        return super.get();
    }
}

// Inheritance must be ordered from “most base-like” to “most derived”.
// Swapping the order of A and B will throw a compilation error.
contract F is A, B {
    function get() public pure override(A, B) returns (string memory) {
        return super.get();
    }
}
```